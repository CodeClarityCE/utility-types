pipeline {
    agent { label 'codeclarity' }

    options {
        timeout(time: 30, unit: 'MINUTES')
        timestamps()
        disableConcurrentBuilds()
        buildDiscarder(logRotator(numToKeepStr: '10'))
    }

    environment {
        GOPRIVATE = 'github.com/CodeClarityCE/*'
    }

    stages {
        stage('Setup') {
            steps {
                sh 'go version'
                script {
                    def config = readJSON file: 'config.json'
                    env.Version = config.version
                    echo "Version: ${Version}"
                }
                sh 'git fetch --tags --force origin'
                sh 'git fetch origin main:refs/remotes/origin/main || true'
            }
        }

        stage('PR Version Check') {
            when {
                expression { env.CHANGE_TARGET == 'main' }
            }
            steps {
                script {
                    def configExists = sh(script: "git show origin/main:config.json > /dev/null 2>&1", returnStatus: true) == 0
                    if (configExists) {
                        def mainConfigJson = sh(
                            script: "git show origin/main:config.json",
                            returnStdout: true
                        ).trim()
                        def mainConfig = readJSON text: mainConfigJson
                        def mainVersion = mainConfig.version
                        if (Version == mainVersion) {
                            error "Version not bumped! Current: ${Version}, Main: ${mainVersion}. Please update config.json."
                        }
                        echo "Version check passed: ${mainVersion} -> ${Version}"
                    } else {
                        echo "No config.json on main branch yet, skipping version check"
                    }
                }
            }
        }

        stage('Dependencies') {
            steps {
                sh 'go mod download'
                sh 'go mod tidy'
            }
        }

        stage('Lint') {
            steps {
                sh 'go fmt ./...'
                sh 'go vet ./...'
            }
        }

        stage('Test') {
            steps {
                sh 'go test -v ./...'
            }
        }

        stage('Release') {
            when {
                branch 'main'
            }
            steps {
                script {
                    def tagExists = sh(script: "git tag -l '${Version}' | grep -q .", returnStatus: true) == 0
                    if (!tagExists) {
                        sh "gh release create '${Version}' --title 'Release ${Version}' --generate-notes --prerelease=\$(echo '${Version}' | grep -q 'alpha\\|beta' && echo true || echo false)"
                    }
                }
            }
        }
    }

    post {
        failure {
            echo "Build failed for utility-types ${env.Version ?: 'unknown'}"
        }
    }
}
